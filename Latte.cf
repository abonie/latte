-- bnfc --functor -m --haskell -o src/parsing/ Latte.cf

-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= FunDef ;

ClsDef.	   TopDef ::= "class" Ident Ext CBody ;

separator nonempty TopDef "" ;

ExtSome.   Ext ::= "extends" Ident ;

ExtNone.   Ext ::= "" ;

FunDef.    FunDef ::= Type Ident "(" [Arg] ")" Block ;

Arg. 	   Arg ::= Type Ident;

separator  Arg "," ;

CBody.     CBody ::= "{" [MemDecl] "}" ;

MemVar.    MemDecl ::= Type [Ident] ";" ;

MemFun.    MemDecl ::= FunDef ;

separator  MemDecl "" ;

separator nonempty Ident "," ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= LHS "=" Expr  ";" ;

Incr.      Stmt ::= LHS "++"  ";" ;

Decr.      Stmt ::= LHS "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

LhsVar.    LHS ::= Ident ;

LhsMem.    LHS ::= Ident "." Ident ;

LhsInd.    LHS ::= Ident "[" Expr "]" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

TCls.      Type ::= Ident ;

Arr.       Type ::= Type "[]" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

EMem.      Expr6 ::= Ident "." Ident ;

EInd.      Expr6 ::= Ident "[" Expr "]" ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

ENew.      Expr6 ::= "new" Ident ;

ENull.     Expr6 ::= "(" Ident ")" "null" ;

EArr.      Expr6 ::= "new" Type "[" Expr "]" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EMet.      Expr6 ::= Ident "." Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;


Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
